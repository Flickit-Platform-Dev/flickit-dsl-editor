grammar org.flickit.dsl.editor.Profile with org.eclipse.xtext.common.Terminals
generate profile "http://www.flickit.org/dsl/editor/Profile"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


AssessmentProfile:
	includes+=Include*
    (elements+=BaseInfo)*;
    
Include:
	'import' importURI=STRING;
 
BaseInfo:
    Profile | Subject | Questionnaire | QualityAttribute | Metric;
	
Profile:
    'Profile' name=ID;

Subject:
    'Subject' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    ('questionnaires:' questionnaires+=[Questionnaire] (',' questionnaires+=[Questionnaire])*)?
    '}';

Questionnaire:
    'Questionnaire' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    '}';

QualityAttribute:
    'QualityAttribute' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('index:' index=INT)?
    'subject:' subject=[Subject]
    '}';
    
    
   
Level:
    'WEAK' | 'MODERATE' | 'GOOD' | 'GREAT' | 'EXCEPTIONAL';
    

AffectsLevel:
    'affects' level=Level 'level' 'of' qualityAttribute=[QualityAttribute] ('with' 'values' '{' values+=FLOAT (',' values+=FLOAT)* '}')?;
  
CustomOption:
	'options' optionFrom+=INT 'to' optionTo+=INT 'affects' 'on' optionLevel+=Level 'level' 'of' qualityAttribute+=[QualityAttribute] ('with' 'values' '{' values+=FLOAT (',' values+=FLOAT)* '}')*
; 

NecessaryCondition:
	'option' necessaryOption+=INT 'is' 'necessary' 'for'  necessaryLevel+=Level 'level' 'of' qualityAttribute+=[QualityAttribute]
	
;

FLOAT:
	INT '.' INT;

Metric:
    'Metric' name=ID '{'
    'questionnaire:' questionnaire=[Questionnaire]
    ('description:' description=STRING)?
    'question:' question=STRING
    'options:' options+=STRING (',' options+=STRING)*
    (affects+=AffectsLevel)*
    (customOptions+=CustomOption)*
    (necessaryCondition+=NecessaryCondition)*
     '}';