grammar org.flickit.dsl.editor.AssessmentKitDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate assessmentKitDsl "http://www.flickit.org/dsl/editor/AssessmentKitDsl"

Root:
	includes+=Include*
    (elements+=BaseInfo)*;
    
Include:
	'import' importURI=STRING;
 
BaseInfo:
    AssessmentKit | Subject | Questionnaire | QualityAttribute | Question | Level;
	
AssessmentKit:
    'AssessmentKit' name=ID '{'
    ('title:' title=STRING)
    levels+=Level*
    '}';
    
    
Level:
    'Level' name=ID '{'
    ('title:' title=STRING)? (',')?
    (competence+=Competence (',' competence+=Competence)*)?
    '}';
    
Competence:
    'competence:' '[' 
    competenceValues+=CompetenceValue (',' competenceValues+=CompetenceValue)* 
    ']';
    
CompetenceValue:
    level=[Level] ':' value=INT '%';

Subject:
    'Subject' name=ID '{'
    ('title:' title=STRING)
    ('code:' code=STRING)?
    ('description:' description=STRING)
    ('index:' index=INT)?
    ('questionnaires:' questionnaires+=[Questionnaire] (',' questionnaires+=[Questionnaire])*)?
    '}';

Questionnaire:
    'Questionnaire' name=ID '{'
    ('title:' title=STRING)
    ('code:' code=STRING)?
    ('description:' description=STRING)
    ('index:' index=INT)?
    '}';

QualityAttribute:
    'QualityAttribute' name=ID '{'
    ('title:' title=STRING)
    ('description:' description=STRING)
    ('code:' code=STRING)?
    ('index:' index=INT)?
    'subject:' subject=[Subject]
    ('weight' weight=INT)? 
    '}';
            

AffectsLevel:
    'affects' level=[Level] 'level' 'of' qualityAttribute=[QualityAttribute] ('with' 'values' '{' values+=FLOAT (',' values+=FLOAT)* '}')?  ('weight' weight=INT)?;
  
CustomOption:
	'options' optionFrom+=INT 'to' optionTo+=INT 'affects' 'on' optionLevel+=[Level] 'level' 'of' qualityAttribute+=[QualityAttribute] ('with' 'values' '{' values+=FLOAT (',' values+=FLOAT)* '}')* ('weight' weight=INT)?
; 

NecessaryCondition:
	'option' necessaryOption+=INT 'is' 'necessary' 'for'  necessaryLevel+=[Level] 'level' 'of' qualityAttribute+=[QualityAttribute]
	
;

FLOAT:
	INT '.' INT;
	
MayNotBeApplicable:
    'true' | 'false';

Question:
    'Question' name=ID '{'
    'questionnaire:' questionnaire=[Questionnaire]
    ('description:' description=STRING)?
    'title:' question=STRING
    'options:' options+=STRING (',' options+=STRING)*
    (affects+=AffectsLevel)*
    (customOptions+=CustomOption)*
    (necessaryCondition+=NecessaryCondition)*
    ('mayNotBeApplicable:' mayNotBeApplicable=MayNotBeApplicable)?
     '}';
